main local.target local.inflictor local.damage local.position local.direction local.normal local.knockback local.damageflags local.meansofdeath local.location local.entity:

	// change the pink text when killed by their own "meansofdeath = fire" weapon; temporarily immolate the player with fire if damaged by a "meansofdeath = fire" weapon.
	// change the pink text when killed by their own rocket launcher, or if knockback >= 250.
	if(local.entity.classname == Player && isalive local.entity && local.entity.dead != 1 && local.entity.dmteam != "spectator" && local.entity.health > 0)
	{
		if(local.knockback >= 350 && local.damage > local.entity.health && local.inflictor == local.entity)
		{
			local.entity damage local.entity 9999999 local.entity local.position local.direction local.normal local.knockback local.damageflags 16 local.location
			local.entity addkills -1
			end
		}

		if(local.meansofdeath == 21)
		{
			if(local.entity == local.inflictor && local.entity.health <= 5)
			{
				local.entity damage $world 9999999 $world (0 0 0) (0 0 0) (0 0 0) 0 9 5 0
				local.entity addkills -1
				end
			}

			local.entity.immolated_c = 0
			if(local.entity.immolated != 1 && local.inflictor.classname == Player && (local.entity.dmteam != local.inflictor.dmteam || getcvar("g_gametype") == "1" || local.entity == local.inflictor))
			{
				local.entity thread immolated
			}
		}
	}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------

	// assisted suicide: give kills to players who damaged someone within 1.5 seconds of them beginning to fall off a cliff (all the way until they crater and die).
	if(local.entity.classname == Player && local.inflictor != NULL && local.inflictor != $world)
	{
		if(local.inflictor != $world && local.inflictor != local.entity)
		{
			if(local.inflictor.player_owner != NIL && local.inflictor.player_owner != NULL) { local.entity.suicide_assister = local.inflictor.player_owner }
			else if(local.inflictor.owner != NIL && local.inflictor.owner != NULL) { local.entity.suicide_assister = local.inflictor.owner }
			else { local.entity.suicide_assister = local.inflictor }

			local.entity.suicide_assist_c = 0
			local.entity.suicide_assist_team = local.entity.dmteam
			local.entity.suicide_assister_team = local.entity.suicide_assister.dmteam
		}
		if((local.inflictor == $world || local.inflictor == local.entity) && local.meansofdeath == 7 && local.entity.suicide_assist_c != NIL && local.entity.suicide_assist_c < 40 && local.entity.suicide_assister != NIL && local.entity.suicide_assister != NULL)
		{
			local.entity damage local.entity.suicide_assister 9999999 local.entity.suicide_assister (0 0 0) (0 0 0) (0 0 0) 0 9 7 0
		}
	}

//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------
//-----------------------------------------------------------------------------

	// only for improving train crashes if damaged by a flying object, bomb, rocket, etc that deals "radiusdamage", causing $world to deal damage to entities.

	if(local.entity.train != NIL && local.entity.train != NULL && local.entity.train.model == "models/vehicles/bp44train.tik" && local.entity.train.train_sequencing == 1)
	{
		local.entity.train.damagerorigin = local.entity.train.origin + local.position
		end
	}

//-----------------------------------------------------------------------------

	// give score to players who destroy nebelwerfers or flak88s with players using them, using explosives of various types (or shotgun-blasting & destroying them).
	if((local.entity.targetname == "flak88_healthtrig" || local.entity.targetname == "nebelwerfer_healthtrig") && local.inflictor != NULL) 
	{
		if(local.entity.driver == NIL || local.entity.driver == NULL || (local.entity.driver != NIL && (local.entity.driver.dmteam != local.inflictor.dmteam || getcvar("g_gametype") == "1"))) { local.entity.shooter = local.inflictor }
		if(local.entity.driver != NIL && local.entity.driver != NULL && local.entity.driver.dmteam == local.inflictor.dmteam) { local.entity.nodamage = 1; local.entity nodamage }
		end
	}

//-----------------------------------------------------------------------------

	// give score to players who destroy vehicles/tanks with a living turret player on them, to prevent the dead tank/vehicle from killing the turret player instead ("takedamage" is turned back on just before they die).
	if(local.entity.classname == Player && local.entity.turretingent != NIL && local.inflictor == local.entity.turretingent) { local.entity nodamage }

	// give score to players who destroy vehicles/tanks with players riding in them, using explosives of various types.
	if((local.entity.classname == DrivableVehicle || local.entity.classname == VehicleTank) && local.inflictor != NULL)
	{
		for(local.i = 1; local.i <= local.entity.passengers.size; local.i++)
		{
			if(local.entity.passengers[local.i] != NIL && local.entity.passengers[local.i].dmteam == local.inflictor.dmteam && getcvar("g_gametype") != "1")
			{
				local.entity.nodamage = 1
				local.entity nodamage
				end
			}
		}
		if((local.entity.driver != NIL && local.entity.driver.dmteam == local.inflictor.dmteam && getcvar("g_gametype") != "1") || (local.entity.turretplayer != NIL && local.entity.turretplayer.dmteam == local.inflictor.dmteam && getcvar("g_gametype") != "1"))
		{
			local.entity.nodamage = 1
			local.entity nodamage
			end
		}

		if((local.inflictor.classname == DrivableVehicle || local.inflictor.classname == VehicleTank) && local.inflictor.shooter != NIL && local.inflictor.shooter != NULL)
		{
			if(local.inflictor.shooter.player_owner != NIL && local.inflictor.shooter.player_owner != NULL) { local.entity.shooter = local.inflictor.shooter.player_owner }
			else if(local.inflictor.shooter.owner != NIL && local.inflictor.shooter.owner != NULL) { local.entity.shooter = local.inflictor.shooter.owner }
			else { local.entity.shooter = local.inflictor.shooter }
		}
		else if(local.inflictor != $world)
		{
			if(local.inflictor.player_owner != NIL && local.inflictor.player_owner != NULL) { local.entity.shooter = local.inflictor.player_owner }
			else if(local.inflictor.owner != NIL && local.inflictor.owner != NULL) { local.entity.shooter = local.inflictor.owner }
			else { local.entity.shooter = local.inflictor }
		}
		end
	}

//-----------------------------------------------------------------------------

	// end if attacker is player_owner's teammate, or player_owner himself.
	if(local.entity.player_owner == NIL || local.entity.player_owner == NULL || local.inflictor == NULL || local.entity.player_owner == local.inflictor || (local.entity.player_owner.dmteam == local.inflictor.dmteam && getcvar("g_gametype") != "1")) { end }

//-----------------------------------------------------------------------------

	// sentry turrets cannot be damaged by bullets, snipers, or shotguns.
	if(local.damage < 95 && local.entity.targetname == "sentry_turret" && (local.meansofdeath == 18 || local.meansofdeath == 19 || local.meansofdeath == 27)) { end }
	if(local.damage < 10 && (local.entity.targetname == "claymore" || local.entity.targetname == "cluster_mine" || local.entity.targetname == "airstrike_bomberplanetrig")) { end }

//-----------------------------------------------------------------------------

	// for players that destroy an enemy's killstreaks.
	local.maxplayers = int(getcvar("sv_maxclients"))
	if(local.inflictor.entnum <= local.maxplayers - 1) { local.entity.enemy_killer = local.inflictor }

//-----------------------------------------------------------------------------

	// do not use "killent", otherwise server will crash due to command overflow.
	if(local.entity.health > 0 && local.entity.targetname != "airstrike_bomberplanetrig") { local.entity.health = 1 }

	if(local.entity.targetname == "airstrike_bomberplanetrig")
	{
		if(local.entity.health > 10000 + local.damage + 1) { local.entity.health -= 10000 }
		else { local.entity.health = 1 }
	}
end

////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////

chain_reaction:

	local.killstreaks = makeArray 				// matrix array starts with [1][1] at top-left. one column down = [2][1]. one row right = [1][2]. rows are separated by spaces only.
		sentry_turret level.sentry_deadradius		// killstreaks.size = # of rows. killstreaks[1].size = # of columns.
		claymore level.claymore_radius
		cluster_mine level.mines_radius
	endArray

	for(local.k = 1; local.k <= local.killstreaks.size; local.k++)
	{
		for(local.i = 1; local.i <= $(local.killstreaks[local.k][1]).size; local.i++)
		{
			if(self != $(local.killstreaks[local.k][1])[local.i] && vector_length(self.origin - $(local.killstreaks[local.k][1])[local.i].origin) <= local.killstreaks[local.k][2])
			{
				$(local.killstreaks[local.k][1])[local.i].enemy_killer = self.enemy_killer
				killent $(local.killstreaks[local.k][1])[local.i].entnum
			}
		}
	}
end

////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////

assisted_suicide:

	while(1)
	{
		for(local.i = 1; local.i <= $player.size; local.i++)
		{
			if($player[local.i] != NULL)
			{
				if($player[local.i].suicide_assister != NIL && $player[local.i].suicide_assister != NULL && $player[local.i].suicide_assist_c != NIL && $player[local.i].suicide_assist_team != NIL && $player[local.i].suicide_assister_team != NIL)
				{
					if(isalive $player[local.i] && $player[local.i].dmteam != "spectator" && $player[local.i].suicide_assist_c < 30 && $player[local.i].dmteam == $player[local.i].suicide_assist_team && $player[local.i].suicide_assister.dmteam == $player[local.i].suicide_assister_team && $player[local.i].suicide_assister.dmteam != "spectator")
					{
						if($player[local.i] getmovement != "falling" || $player[local.i].using_pack == 1) { $player[local.i].suicide_assist_c++ }
					}
					else
					{
						$player[local.i].suicide_assist_c = NIL
						$player[local.i].suicide_assist_team = NIL
						$player[local.i].suicide_assister = NIL
						$player[local.i].suicide_assister_team = NIL
					}
				}
				else
				{
					$player[local.i].suicide_assist_c = NIL
					$player[local.i].suicide_assist_team = NIL
					$player[local.i].suicide_assister = NIL
					$player[local.i].suicide_assister_team = NIL
				}
			}
		}
		waitframe
	}
end

////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////

immolated:

	local.team = self.dmteam
	self.immolated = 1
	if(self.immolation != NIL && self.immolation != NULL) { self.immolation remove }

	local.fire = spawn script_model
	local.fire model "emitters/explosion_conflagration.tik"
	local.fire anim start
	local.fire notsolid
	local.fire.scale = 0.25
	local.fire loopsound snowballfire
	self.immolation = local.fire

	while(self != NULL && isalive self && self.dmteam == local.team && self.dmteam != "spectator" && self.dead != 1 && self.missile == NIL && self.immolated == 1 && self.immolated_c <= 2)
	{
		if(self.immolation != NIL && self.immolation != NULL)
		{
			if(self.dog != NIL && self.dog != NULL) { self.immolation.origin = self.dog gettagposition "tag_mouth" }
			else { self.immolation.origin = self gettagposition "Bip01 Head" }
		}
		self.immolated_c++
		waitframe
	}

	if(self != NULL) { self.immolated_c = 0 }
	while(self != NULL && !isalive self && self.dmteam == local.team && self.dmteam != "spectator" && self.immolated == 1 && self.immolated_c <= 80) // immolate dead bodies for 4 seconds.
	{
		if(self.immolation != NIL && self.immolation != NULL)
		{
			if(self.dog != NIL && self.dog != NULL) { self.immolation.origin = self.dog gettagposition "tag_mouth" }
			else { self.immolation.origin = self gettagposition "Bip01 Head" }
		}
		local.died = 1
		self.immolated_c++
		waitframe
	}

	if(self != NULL) { self.immolated = 0; self.immolated_c = 0 }
	if(local.fire != NIL && local.fire != NULL) { local.fire remove }
end
