main local.origin local.angles local.name local.offtime local.damageradius local.pulsatingoff local.manualreset:

exec global/aliascache_triggersounds.scr alarm_switch

local.switch = spawn script_model
local.switch model "animate/alarmswitch.tik"
local.switch.origin = local.origin
local.switch.angles = local.angles
local.switch notsolid

local.switchtrig = spawn trigger_use
local.switchtrig.origin = local.switch.origin
local.switchtrig setsize ( -20 -20 -20 ) ( 20 20 20 )

local.switchtrig waittill trigger
local.switchtrig nottriggerable
local.switch anim turnon
local.switch playsound alarmswitch

/////////////////////////////////////////////*** add code here ***//////

 	local.player = parm.other
	if(local.name.size > 1 && local.name[1].size > 1)
	{
		local.player iprint ("*** Blowing up some bangalores. ***")

		if(local.name[0] != NIL && local.name[0].size > 0) { local.sub = 1 } // works if the first local.name starts at either local.name[0] or local.name[1].
		else { local.sub = 0 }

		wait (randomint(50) / 50.00 + 0.25)
		for(local.n = 0; local.n < local.name.size - local.sub; local.n++)
		{
			if(local.name[local.n].size > 0)
			{
				local.player thread maps/training_mapscripts/bangalore_wallbomb.scr::blowup local.name[local.n] local.offtime local.damageradius local.pulsatingoff
				wait (randomint(100) / 100.00 + 0.25)
			}
		}
		local.player waitthread maps/training_mapscripts/bangalore_wallbomb.scr::blowup local.name[local.name.size - local.sub] local.offtime local.damageradius local.pulsatingoff
	}
 	else
	{
		local.player iprint ("*** Blowing up a bangalore. ***")
		local.player waitthread maps/training_mapscripts/bangalore_wallbomb.scr::blowup local.name local.offtime local.damageradius local.pulsatingoff
	}

////////////////////////////////////////////////////////////////////////

if(local.manualreset != 1)
{
 local.switch anim turnoff
 wait 1
}
if(local.manualreset == 1)
{
 local.switchtrig triggerable
 local.switchtrig waittill trigger
 local.switch anim turnoff
 local.switch playsound alarmswitch

/////////////////////////////////////////////*** add code here ***//////

 	local.player = parm.other
 	local.player iprint ("*** Resetting the alarm switch. ***")
	wait 1

////////////////////////////////////////////////////////////////////////
}

local.switch remove
local.switchtrig remove

thread main local.origin local.angles local.name local.offtime local.damageradius local.pulsatingoff local.manualreset
end