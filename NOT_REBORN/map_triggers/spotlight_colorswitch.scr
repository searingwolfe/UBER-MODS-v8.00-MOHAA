// This script spawns in an alarm switch.
// When triggered, the spotlight with the same targetname as "name" will change colors only after the player is close enough to rotate the spotlight (while looking around).
//
// If the colorswitch's "name2" parameter = "rainbow", then the spotlight's color will change continuously, only while the player is moving the spotlight.
// Similarly if "name2" = "slowrainbow", then the spotlight's color will change continuously but slower, only while the player is moving the spotlight.
// Once the player walks too far away, the spotlight stops changing colors. Ideally, the spotlight colorswitch should be placed near the spotlight whose beam-color will be changed.
// 
// If name2 is empty or NIL, then triggering the alarmswitch will change spotlight's color to a new random color one time, and the alarm switch turns off.
// If name2 = "rainbow", then triggering the alarmswitch will turn on a rapid randomization of the spotlight's color, and triggering the alarmswitch again will turn off the rapid color switching.
// If name2 = "slowrainbow", then triggering the alarmswitch will turn on a slower randomization of the spotlight's color, and triggering the alarmswitch again will turn off the slower color switching.
// If name = "all", then all spotlights on the map will have their beam-colors change one time.
// If name = "all" and name2 = "rainbow", then all spotlights on the map will have their beam-colors changing rapidly and continuously until the colorswitch is triggered again.
// Multiple spotlight_colorswitches can be used for one spotlight.
//
// Parameters: origin, angles, name, name2. 
// Examples:
//	exec map_triggers/spotlight_colorswitch.scr ( -6511 -3072 2849 ) ( 0 107 0 ) s0 rainbow
//	exec map_triggers/spotlight_colorswitch.scr ( -2134 -1164 1432 ) ( 0 107 0 ) s1 slowrainbow
//	exec map_triggers/spotlight_colorswitch.scr ( -3924 1365 1138 ) ( 0 180 0 ) s2
//	exec map_triggers/spotlight_colorswitch.scr ( -954 -4715 729 ) ( 270 123 0 ) s3

main local.origin local.angles local.name local.name2:

exec global/aliascache_triggersounds.scr alarm_switch

local.switch = spawn script_model targetname ("s_colorswitch_" + local.name)
local.switch model "animate/alarmswitch.tik"
local.switch.origin = local.origin
local.switch.angles = local.angles
local.switch notsolid

local.switchtrig = spawn trigger_use targetname ("s_colorswitchtrig_" + local.name)
local.switchtrig.origin = local.switch.origin
local.switchtrig setsize ( -20 -20 -20 ) ( 20 20 20 )

local.switchtrig waittill trigger
local.switchtrig nottriggerable
local.switch anim turnon
local.switch playsound alarmswitch

if(local.name == NIL || (local.name == "all" && local.name2 == NIL)) { level.flags[allspotlights] = 1 }
if(local.name == "all")
{
	if(local.name2 == "slowrainbow") { level.flags[allspotlights_rainbow] = 3 } // "= 2" might already be used by some alarmswitch scripts in M1L2A or M1L2B auto-spotlights.
	else { level.flags[allspotlights_rainbow] = 1 }
}

if(local.name2 != "rainbow" && local.name2 != "slowrainbow")
{
	level.spotlight[local.name] = 1 

	if(local.name != "all") { local.switchtrig message ("*** Randomizing a new spotlight color. ***") }
	if(local.name == "all") { local.switchtrig message ("*** Randomizing a new spotlight color for all spotlights. ***") }
	wait 1
	local.switch anim turnoff
	wait 1
}
else
{
	if(local.name2 == "slowrainbow") { level.spotlight_rainbow[local.name] = 3 }
	else { level.spotlight_rainbow[local.name] = 1 }

	if(local.name != "all" && local.name2 != "slowrainbow") { local.switchtrig message ("*** Rainbow spotlight colors turned ON. ***") }
	if(local.name == "all" && local.name2 != "slowrainbow") { local.switchtrig message ("*** Rainbow spotlight colors turned ON for all spotlights. ***") }
	if(local.name != "all" && local.name2 == "slowrainbow") { local.switchtrig message ("*** Slow rainbow spotlight colors turned ON. ***") }
	if(local.name == "all" && local.name2 == "slowrainbow") { local.switchtrig message ("*** Slow rainbow spotlight colors turned ON for all spotlights. ***") }
	local.player = parm.other
	local.switchtrig triggerable

	local.switchtrig waittill trigger
	if(local.name != "all" && local.name2 != "slowrainbow") { local.switchtrig message ("*** Rainbow spotlight colors turned OFF. ***") }
	if(local.name == "all" && local.name2 != "slowrainbow") { local.switchtrig message ("*** Rainbow spotlight colors turned OFF for all spotlights. ***") }
	if(local.name != "all" && local.name2 == "slowrainbow") { local.switchtrig message ("*** Slow rainbow spotlight colors turned OFF. ***") }
	if(local.name == "all" && local.name2 == "slowrainbow") { local.switchtrig message ("*** Slow rainbow spotlight colors turned OFF for all spotlights. ***") }
	local.switchtrig nottriggerable

	local.switch playsound alarmswitch
	local.switch anim turnoff
	level.spotlight_rainbow[local.name] = 0
	if(local.name == "all") { level.flags[allspotlights_rainbow] = 0 }
	wait 1
}

local.switch remove
local.switchtrig remove

thread main local.origin local.angles local.name local.name2
end