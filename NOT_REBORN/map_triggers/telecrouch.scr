// parameters: origin, destination, tele type (0 = one-way tele, 1 = two-way), ...
// destination_crouch (0 = make player stand, 1 = crouch player at destination), origin_crouch (0 = make player stand, 1 = crouch player at origin)

main local.origin local.destination local.teletype local.destination_crouch local.origin_crouch local.targetname:

	local.trig = spawn trigger_use
	local.trig.origin = local.origin + ( 0 0 10 )
	local.trig.destination = local.destination
	local.trig model "fx/corona_red.tik"
	local.trig setsize ( -10 -10 -10 ) ( 10 10 0 )
	local.trig.scale = 1.5
	local.trig notsolid
	local.trig show
	local.trig setthread tele
	local.trig.origin_crouch = local.origin_crouch
	local.trig.destination_crouch = local.destination_crouch
	if(local.targetname != NIL) { local.trig.targetname = local.targetname }

	if(local.teletype == 1)
	{
		thread main local.destination local.origin 0 local.origin_crouch local.destination_crouch local.targetname
	}
end

tele:
	local.player = parm.other
	local.player tele self.destination

	if(self.destination_crouch == 1) { local.player forcelegsstate CROUCH_IDLE }
	else { local.player forcelegsstate STAND }
end