main: // find.scr. Run "exec global/find.scr" in any map script to use the "find [targetname without $]" cvar. For multiple entities with same targetname: "find barrel[2]"

	while(string(getcvar("find")) == "") { waitframe }
	local.targetname = string(getcvar("find"))

	if(local.targetname.size > 3 && local.targetname[local.targetname.size - 1] == "]")
	{
		if(local.targetname[local.targetname.size - 3] == "[")
		{
			local.arrayint = int(string(local.targetname[local.targetname.size - 2]))
			local.subtract = 0
		}
		if(local.targetname[local.targetname.size - 4] == "[")
		{
			local.arrayint = int(string(local.targetname[local.targetname.size - 3]) + string(local.targetname[local.targetname.size - 2]))
			local.subtract = 1
		}
		if(local.targetname[local.targetname.size - 5] == "[")
		{
			local.arrayint = int(string(local.targetname[local.targetname.size - 4]) + string(local.targetname[local.targetname.size - 3]) + string(local.targetname[local.targetname.size - 2]))
			local.subtract = 2
		}

		local.arrayname = string("")
		local.multiprint = 1

		for(local.a = 0; local.a < (local.targetname.size - 3 - local.subtract); local.a++)
		{
			local.arrayname = local.arrayname + string(local.targetname[local.a])
		}
	}
	if(local.arrayint == NIL && $(local.targetname) == NULL)
	{
		println("----- ERROR. Entity: $" + local.targetname + " does not exist!")
		if($player.size >= 1) { $player iprint ("----- ERROR. Entity: $" + local.targetname + " does not exist!") }
	}

	if($(local.targetname) != NULL)
	{
		if($(local.targetname).size > 1)
		{
			println("----- NOTE. Multiple entities named: $" + local.targetname)
			if($player.size >= 1) { $player iprint ("----- NOTE. Multiple entities named: $" + local.targetname) }

			for(local.e = 1; local.e <= $(local.targetname).size; local.e++)
			{
				println("----- $" + local.targetname + "[" + local.e + "]")
				if($player.size >= 1) { $player iprint ("----- $" + local.targetname + "[" + local.e + "]") }
			}
		}
		if($(local.targetname).size == 1 && local.multiprint != 1)
		{
			exec cvars/cvars.scr::entprint local.targetname

			for(local.i = 1; local.i <= $player.size; local.i++)
			{
				if(isalive $player[local.i]) 
				{
					$player[local.i] tele $(local.targetname).origin
					$player[local.i] face ( $(local.targetname).angles[0] $(local.targetname).angles[1] 0 )

					if($(local.targetname).classname == PlayerIntermission) { $player[local.i].origin += ( 0 0 -95 ) }
				}
			}
		}
	}
	if(local.arrayint != NIL)
	{
		if($(local.arrayname).size < local.arrayint)
		{
			println("----- ERROR. Entity: $" + local.arrayname + "[" + local.arrayint + "] does not exist!")
			if($player.size >= 1) { $player iprint ("----- ERROR. Entity: $" + local.arrayname + "[" + local.arrayint + "] does not exist!") }
		}

		if($(local.arrayname).size >= local.arrayint)
		{
			exec cvars/cvars.scr::entprint local.arrayname local.arrayint

			for(local.i = 1; local.i <= $player.size; local.i++)
			{
				if(isalive $player[local.i]) 
				{
					$player[local.i] tele $(local.arrayname)[local.arrayint].origin
					$player[local.i] face ( $(local.arrayname)[local.arrayint].angles[0] $(local.arrayname)[local.arrayint].angles[1] 0 )

					if($(local.arrayname)[local.arrayint].classname == PlayerIntermission) { $player[local.i].origin += ( 0 0 -95 ) }
				}
			}
		}
	}
	if(local.targetname != "") { setcvar "find" "" }
  	thread main
end
