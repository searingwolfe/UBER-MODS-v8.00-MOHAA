// To allow players to spawn with 1+ throwing knives, the "global/throwingknife.scr" script is required.
// After "level waittill spawn", add the line below to spawn players with 1 knife.
//
//	exec global/give_players_knives.scr 1 // number of knives (max = 3).
//
// Reborn/Nightfall only: 	Players can throw knives by holding USE(e) key + Secondary FIRE(zoom).
// original MOH game: 		Players can throw knives by holding USE(e) key + FIRE.

main local.knivesheld:

	if(level.knife_resettime == NIL || level.knife_resettime <= 0) { level.knife_resettime = 45 } // without this, the 1st thrown knife (on a map without extra knives spawned somewhere) will have no velocity.
	if(level.knife_velocity == NIL || level.knife_velocity <= 0) { level.knife_velocity = 1400 }

	if(local.knivesheld == NIL || local.knivesheld <= 0) { local.knivesheld = 1 } // players can only have 1, 2, or 3 knives after respawning.
	if(local.knivesheld > 3) { local.knivesheld = 3 }

	if(level.throwingknife_scriptmaster != 1) { exec global/throwingknife.scr::initialize }
	level.throwingknife_scriptmaster = 1

	while($player.size < 1) { waitframe } // don't run any $player.size for-loops until at least 1 player has joined.
	while(1)
	{
		for(local.c = 1; local.c <= $player.size; local.c++)
		{
			if(isalive $player[local.c] && $player[local.c].dmteam != "spectator" && $player[local.c].knivesheld == NIL) // player came out of spectator for 1st time.
			{				
				$player[local.c] thread spawn_with_knives local.knivesheld
			}
			if($player[local.c] != NULL && (!isalive $player[local.c] || $player[local.c].dead == 1) && $player[local.c].dmteam != "spectator" && $player[local.c].knivesheld != NIL && $player[local.c].knivesheld < 1) // player died, increment knivesheld.
			{				
				$player[local.c] thread spawn_with_knives local.knivesheld
			}
			if($player[local.c].dmteam == "spectator") { $player[local.c].knivesheld = NIL }
		}
		waitframe
	}
end

spawn_with_knives local.knivesheld:

	self.knivesheld = local.knivesheld

	while(self != NULL && (!isalive self || self.dead == 1) && self.dmteam != "spectator") { waitframe } // wait until players are alive again, before giving them knives again.

	if(self != NULL) { self thread global/throwingknife.scr::give_knife NIL NIL NIL } // NILs are used since these knives once thrown have no initial origins/names for resetting them.
end