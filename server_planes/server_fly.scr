main local.modelpair:
	
	if(self.flying == 1)
	{
		self.scale = 1
		end
	}

	if(self.OWNS == 1)
	{
		self.scale = 1
		self.flying = 0
		end
	}

	self.no_usemine = 1 //stop them using a mine
	self.no_usemedic = 1 //stop medics

	self.flying = 1
	self.flying_init = 1
	self takedamage

	local.team = self.dmteam
	self.modelpair = local.modelpair

	if(level.planes_max["allies"]== NIL)
	{
		waitexec server_planes/setup.scr
	}

	level.planes[local.team]++

	local.allies = waitexec global/settings.scr::getcmd "max-allies"
	local.axis = waitexec global/settings.scr::getcmd "max-axis"

	level.planes_max["allies"] = int local.allies 
	level.planes_max["axis"] = int local.axis

	if(level.planes[local.team] == level.planes_max[local.team])
	{
		exec server_planes/trigger.scr::from_fly local.team
	}

	self hide
	if(self.instructed!=1)
	{
		self waitexec server_planes/first_flight_instructions.scr local.team
	}

	if(self == NULL || self getconnstate == 1) { end }
	if(self.dmteam != local.team || level.change_team_score == 1)
	{
		self.flying = 0
		self.flying_init = 0
		self.drivingent = NIL
		self.no_usemine = 0
		self.no_usemedic = 0
		self physics_on
		if(self.dmteam != "spectator")
		{
			self show
			self solid
		}
		end
	}

	//spawn the plane
	local.plane = spawn script_model "targetname" ("flyingplane" + self.entnum)
	local.plane.origin = self.plane_start
	local.plane.scale = self.scale
	self.flying_init = 0

	if(local.modelpair != 1 && local.modelpair != 2) // default
	{
		if(self.dmteam == "axis") { local.plane model "vehicles/fockwulffly.tik" }
		else { local.plane model "vehicles/p47fly.tik" }
	}
	if(local.modelpair == 1)
	{
		if(self.dmteam == "axis") { local.plane model "vehicles/stuka_fly.tik" }
		else { local.plane model "vehicles/stuka_dsrt_fly.tik" }
	}
	if(local.modelpair == 2)
	{
		local.plane model "vehicles/c47fly.tik"
	}

	if(local.plane.model == "models/vehicles/c47fly.tik") { local.radius = 900 * local.plane.scale }
	else { local.radius = 450 * local.plane.scale }

	if(self.dmteam == "axis") { local.plane light 1 0 0 local.radius }
	else { local.plane light 0 0 1 local.radius }

	if(self.seat == NIL || local.plane.scale != self.oldplane_scale)
	{
		self.oldplane_scale = local.plane.scale
		self.seat[0] = -400.000 * (local.plane.scale) - 45.000 // seat[0] = distance behind plane; seat[1] = height +above / -below plane
		self.seat[1] = 100.00 * (local.plane.scale) - 75.000
	}

	local.plane touchtriggers

	local.plane physics_on
	local.plane.gravity = 0

	//set the plane
	self.plane = local.plane

	//hmm
	local.plane takedamage

	self.maxspeed = waitexec global/settings.scr::getcmd "maxspeed"
	self.minspeed = waitexec global/settings.scr::getcmd "minspeed"
	local.speed =  waitexec global/settings.scr::getcmd "startspeed"
	self.candrop =  waitexec global/settings.scr::getcmd "plane-bombs"
	self.rocket_ammo =  waitexec global/settings.scr::getcmd "rockets"
	level.plane_hit_damage =  waitexec global/settings.scr::getcmd "plane-hit-dmg"
	self.acceleration =  waitexec global/settings.scr::getcmd "acceleration"
	self.deceleration =  waitexec global/settings.scr::getcmd "deceleration"
	self.health = waitexec global/settings.scr::getcmd "plane-bots-damage"

	self.acceleration = int self.acceleration
	self.deceleration = int self.deceleration

	//set start speed
	waitexec server_planes/speedup.scr local.speed self

	self.maxspeed = int self.maxspeed 
	self.minspeed = int self.minspeed 
	self.candrop = int self.candrop
	self.rocket_ammo  = int self.rocket_ammo 

	self solid
	self forcetorsostate PLANE_TORSO
	self forcelegsstate STAND
	self hide
	self.scale = 1
	//self gravity 0
	self.planeweap = 1 
	self.rocketside=1
	self.firingrocket=0
	self.spawnedmg = 0
	self.extraangle = 0
	self.in_para_drop=0
	self.shooting=0	
	self.shootdelay = 0.10
	self.changedelay = .3
	self.canchange = 1 
	self.ejecting=0	

	//announce Player in plane
	local.team  = waitexec global/strings.scr::to_upper local.team  0
	iprintlnbold ("*** " + local.team + " Aircraft Spotted On Radar!!! ***")
	local.team = self.dmteam
	local.entitynum = self.entnum

	local.plane.angles = self.viewangles
//main loop
	self exec server_planes/plane_crash.scr::plane_crash
	self waitexec server_planes/damage.scr::shotdown_setup //setup shot by trigger

	self.drivingent = $("planehit" + self.entnum)

	local.plane loopsound fly_snd
	//self exec server_planes/avatar.scr

	self.z = 0
	while(isalive self && self.dmteam == local.team && self.ejecting != 1 && self.flying == 1 && local.plane != NIL && local.plane != NULL && level.change_team_score != 1)
	{
		if(self==NIL || self==NULL)
		{
			end
		}

		self waitthread playerkeep

/// set planes angles



		if(self.viewangles[1] != local.oldangle)
		{
			local.viewvec = angles_toforward self.viewangles
			local.leftdot = vector_dot local.viewvec self.leftvector
			local.right = 0
			local.right -= local.leftdot
			
			self.z = self.z  + local.right * 10

			local.newangle = local.right * 100 // * 200 //350 //400 //50


			local.newangle =  self.viewangles[2] + local.newangle
			local.plane.angles = ( self.viewangles[0]  self.viewangles[1] local.newangle )
			
			local.newangle =  local.newangle / 10

			
			self.viewangles = ( self.viewangles[0]  self.viewangles[1] self.z )

		}
		else
		{
			
			if(self.z > 10 || self.z < -10 )
			{
				if(self.z >=1)
				{ 
					if(self.z > 180)
					{
						self.z += 10
					}
					else
					{
						self.z -= 10
					}
				}
				else if(self.z <= -1)
				{
					if(self.z < -180)
					{
						self.z -= 10
					}
					else
					{
						self.z += 10
					}
				}
			}
			else
			{
				if(self.z >=1)
				{ 
					self.z -= 1
				}
				else if(self.z <= -1)
				{
					self.z+= 1
				}
			}

			self.viewangles = ( self.viewangles[0]  self.viewangles[1] self.z )

			if(local.plane != NULL) { local.plane.angles = self.viewangles }
			
		}

		local.oldangle = self.viewangles[1]

		if(self.z >= 360)
		{
			self.z = 0
		}
		else if(self.z <= -360)
		{
			self.z = 0
		}


		if(local.plane != NULL) { local.plane.velocity = local.plane.forwardvector * self.speed }

		if(self.useheld==1 && self.canchange == 1)
		{
			self exec server_planes/damage.scr "CHANGE_WEAPON"
		}
		
		
		if(self.fireheld==1 && self.secfireheld != 1 && self.shooting != 1)
		{
			//local.move = self getmovement
			//if(local.move != "walking")
			//{
				self exec server_planes/damage.scr "SHOOT"	
			//}
		}
				
		waitframe

//colision

		if(self != NULL)
		{
			if(local.origin == self.origin)
			{
				self exec server_planes/plane_crash.scr::plane_explode local.plane.scale
				killent self.entnum
				end
			}
			local.origin = self.origin
		}
	}

////// END OF MAIN LOOOP

	level.planes[local.team]--

	if(self != NULL)
	{
		self.drivingent = NIL
		self.no_usemine = 0 //can use a mine again
		self.no_usemedic = 0 //
		if(isalive self) { self.health = 100 } // bot setup

		self exec server_planes/plane_crash.scr::plane_crashing //crash sequence

		if(self.plane != NIL && self.plane != NULL && level.change_team_score != 1)
		{	
			waitframe
			if(self != NULL && self.plane != NIL && self.plane != NULL && level.change_team_score != 1) { self.origin = self.plane.origin + ( 0 10 100 ) }
		}
		else { self thread forcekill }
	}
	else
	{
		if($("planehit" + local.entitynum) != NULL) { $("planehit" + local.entitynum) remove }
		local.plane exec server_planes/plane_crash.scr::plane_crashing_null //crash sequence if player left while flying
	}
end


playerkeep:

	if(self != NULL && self.plane != NIL && self.plane != NULL)
	{
		local.offset = self.plane.origin + ( 0 0 self.seat[1] ) + self.plane.forwardvector  * self.seat[0] + 0.05 * self.plane.forwardvector * self.speed
		self.origin = local.offset
	}
end

forcekill: // rare cases if player crashes plane but doesn't die (i.e. if player was noclipping while flying)

	local.team = self.dmteam
	while(isalive self && self.dmteam == local.team && level.change_team_score != 1)
	{
		killent self.entnum
		wait 0.5
	}
end

