// Auto-respawn players that get stuck inside walls/entities/other players when spawning. Run this from global/dmprecache.scr (not required).
//
// All players who recently spawned will have 2 timers counting before any respawns occur: alive-time & stuck-time.
// If players are alive and stuck in a wall for 3+ seconds ("falling", but unable to move), they will auto-respawn.
// If they do move within 3 seconds and are not "falling", respawn_stuck_fix.scr will turn off for that player until they've respawned after dying, switching teams, or spawning after the map changes.

main:

	while($player.size < 1) { waitframe }
	while($player.size >= 1)
	{
		for(local.i = 1; local.i <= $player.size; local.i++)
		{
			if($player[local.i] != NULL && $player[local.i].dmteam != "spectator" && $player[local.i].dead != 1 && (!isalive $player[local.i] || $player[local.i].fellundermap == 1 || $player[local.i].respawn_stuck_check == NIL || $player[local.i].respawn_stuck_check == 0) && $player[local.i].wallstuck_fixing != 1)
			{
				$player[local.i] thread respawn_stuck_check
			}
		}
		waitframe
	}
	thread main
end

respawn_stuck_check:

	self.respawn_stuck_check = 1
	local.team = self.dmteam

	while(self != NULL && self.dmteam == local.team && (!isalive self || self.fellundermap == 1 || self.respawn_stuck_check == NIL || self.respawn_stuck_check == 0)) { waitframe }
	if(self == NULL) { end }

	local.c = 0
	local.alivetime = 0
	local.origin = self.origin

	while(isalive self && self.dmteam != "spectator" && self.dmteam == local.team && self.dead != 1 && local.c < 30 && local.alivetime < 30 && self.origin == local.origin && self.wallstuck_fixing != 1)
	{
		if(self getposition == "offground") { local.c++ }
		else { local.c = 0; local.alivetime++ }
		wait 0.1
	}

	if(self != NULL)
	{
		if(isalive self && self.dmteam != "spectator" && self.dmteam == local.team && local.c >= 30 && self.flying != 1 && self.driving != 1 && self.passenger != 1 && self.turreting != 1 && self.missile == NIL && self.wallstuck_fixing != 1)
		{
			self iprint "Stuck for 3+ seconds. Respawning..."
			//if(self.admin_name_priority == 1 || self isadmin == 1) { self iprint "This can be disabled in global/dmprecache.scr." }

			if(self.dmteam == "allies" && $allied_spawn != NULL && $allied_spawn.size >= 1)
			{
				local.r = randomint($allied_spawn.size - 1) + 1
				self tele ($allied_spawn[local.r].origin + ( 0 0 10 ))		// use this instead of "respawn" in singleplayer maps, since some spawns may cause the player to be shifted too far in a random direction.
				self.viewangles = $allied_spawn[local.r].angles
			}
			if(self.dmteam == "axis" && $axis_spawn != NULL && $axis_spawn.size >= 1)
			{
				local.r = randomint($axis_spawn.size - 1) + 1
				self tele ($axis_spawn[local.r].origin + ( 0 0 10 ))
				self.viewangles = $axis_spawn[local.r].angles
			}
			if($allied_spawn == NULL || $axis_spawn == NULL) { self respawn }
			local.respawned = 1
		}

		if(local.respawned != 1) { wait 3 }
		if(self != NULL) { self.respawn_stuck_check = 0 }
	}
end