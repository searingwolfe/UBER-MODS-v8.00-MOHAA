// This script serves as a standalone file for any "registerev intermission" threads from other mods (currently: cvars/adminstuff.scr & global/victory_podium.scr).
//
// Using MOHAA_server_auto-reopen_Windows.bat script (available only from xNULL website download for UBER MODS),
// this script can also indicate whether the server crashed and closed, or crashed and is hanging with a Runtime error.
// Runtime errors almost always occur when the server is changing maps (currently running "intermission", but not necessarily caused by it).
// If the server crashes, errors will appear in this standalone script, printing at minimum: "(global/intermission_serverautoreopen.scr," to qconsole.log just before hanging or closing.

main local.type: // erase all playernames from the previous map, then save all players' names and IPs in the current map just before a new map loads (or current one restarts).

	if(local.type == NIL || int(local.type) != 0)
	{
		local.ret = fremove "main/cvars/playernames.txt"
		local.result = fnewdir "main/cvars"
		local.file = fopen "main/cvars/playernames.txt" "a+"
		local.return = fseek local.file 0 2
		if($player != NULL)
		{
			for(local.i = 1; local.i <= $player.size; local.i++)
			{
				if($player[local.i].IP_address_init != NIL)
				{
					local.result = fputs local.file ("name=" + netname $player[local.i] + "\n" + "IP=" + $player[local.i].IP_address_init + "\n")
				}
			}
		}
		local.result = fclose local.file
		end
	}

	if(level.time == NIL || int(level.time) < 60) { end }
	if($intermission_camera == NULL) { wait 1; println("----- ERROR victory_podium.scr: $intermission_camera could not be found!"); end }
	if($victory_podium == NULL) { wait 1; println("----- ERROR victory_podium.scr: $victory_podium could not be found!"); end }

	exec global/victory_podium.scr::map_ended $victory_podium[1].origin $victory_podium[1].angle 0 300 2.5 0.8
end