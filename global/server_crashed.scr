// Simple script that writes the server's current map to "cvars/mapname.txt".
// When the server crashes, the map will be changed to the "next map" after the map that was running when the server last crashed (full list in "nextmap" thread).
// For example, if the server crashed on dm/mohdm6, the server will auto-reopen and change to dm/mohdm7 (instead of staying on the map initially ran by server.cfg).
//
// This script can run with other server auto-reopen scripts. Whenever the server closes & then reopens, the server will auto-change maps to the "next map".
// This script only works in Reborn / Nightfall.
//
// Add [seta server_crashed "1"] in server.cfg.
// Add "exec global/server_crashed.scr" in global/dmprecache.scr for this script to work.

main:

	while(getcvar("server_crashed") == "") { waitframe }

	if(getcvar("server_crashed") != "1")
	{
		local.string = getcvar("mapname")

		local.ret = fremove "main/cvars/mapname.txt" // overwrite the old map name.
		local.result = fnewdir "main/cvars"
		local.file = fopen "main/cvars/mapname.txt" "a+"
		local.return = fseek local.file 0 2
		local.result = fputs local.file local.string
		local.result = fclose local.file // always close files after opening them.
		end
	}
	setcvar "server_crashed" "0"

	local.ret = fexists "main/cvars/mapname.txt"
	if(local.ret != 1) { local.result = fnewdir "main/cvars" }
	local.file = fopen "main/cvars/mapname.txt" "a+"
	local.content = freadall local.file // put all text into a single string (including the enter keys, which are "\n". Tabs are "\t".)
	local.result = fclose local.file

	if(local.content == "") { end }
	local.nextmap = waitthread nextmap local.content

	wait 1
	println("----- Switching to map '" + local.nextmap + "' since the server previously ended after map '" + local.content + "'.")
	wait 2
	stuffsrv("map " + local.nextmap)
end

nextmap local.map:

	switch(local.map)
	{
		case "dm/mohdm1": local.nextmap = "dm/mohdm2"; break
		case "dm/mohdm2": local.nextmap = "dm/mohdm3"; break
		case "dm/mohdm3": local.nextmap = "dm/mohdm4"; break
		case "dm/mohdm4": local.nextmap = "dm/mohdm5"; break
		case "dm/mohdm5": local.nextmap = "dm/mohdm6"; break
		case "dm/mohdm6": local.nextmap = "dm/mohdm7"; break
		case "dm/mohdm7": local.nextmap = "obj_obj_team1"; break

		case "obj/obj_team1": local.nextmap = "obj/obj_team2"; break
		case "obj/obj_team2": local.nextmap = "obj/obj_team3"; break
		case "obj/obj_team3": local.nextmap = "obj/obj_team4"; break
		case "obj/obj_team4": local.nextmap = "training"; break

		case "training": local.nextmap = "m1l1"; break

		case "m1l1": local.nextmap = "m1l2a"; break
		case "m1l2a": local.nextmap = "m1l2b"; break
		case "m1l2b": local.nextmap = "m1l3a"; break
		case "m1l3a": local.nextmap = "m1l3b"; break
		case "m1l3b": local.nextmap = "m1l3c"; break
		case "m1l3c": local.nextmap = "m2l1"; break
		case "m2l1": local.nextmap = "m2l2a"; break
		case "m2l2a": local.nextmap = "m2l2b"; break
		case "m2l2b": local.nextmap = "m2l2c"; break
		case "m2l2c": local.nextmap = "m2l3"; break
		case "m2l3": local.nextmap = "m3l1a"; break
		case "m3l1a": local.nextmap = "m3l1b"; break
		case "m3l1b": local.nextmap = "m3l2"; break
		case "m3l2": local.nextmap = "m3l3"; break
		case "m3l3": local.nextmap = "m4l0"; break
		case "m4l0": local.nextmap = "m4l1"; break
		case "m4l1": local.nextmap = "m4l2"; break
		case "m4l2": local.nextmap = "m4l3"; break
		case "m4l3": local.nextmap = "m5l1a"; break
		case "m5l1a": local.nextmap = "m5l1b"; break
		case "m5l1b": local.nextmap = "m5l2a"; break
		case "m5l2a": local.nextmap = "m5l2b"; break
		case "m5l2b": local.nextmap = "m5l3"; break
		case "m5l3": local.nextmap = "m6l1a"; break
		case "m6l1a": local.nextmap = "m6l1b"; break
		case "m6l1b": local.nextmap = "m6l1c"; break
		case "m6l1c": local.nextmap = "m6l2a"; break
		case "m6l2a": local.nextmap = "m6l2b"; break
		case "m6l2b": local.nextmap = "m6l3a"; break
		case "m6l3a": local.nextmap = "m6l3b"; break
		case "m6l3b": local.nextmap = "m6l3c"; break
		case "m6l3c": local.nextmap = "m6l3d"; break
		case "m6l3d": local.nextmap = "m6l3e"; break
		case "m6l3e": local.nextmap = "void"; break
		case "void": local.nextmap = "dm/mohdm1"; break

		default: local.nextmap = "dm/mohdm6"; break
	}
end local.nextmap