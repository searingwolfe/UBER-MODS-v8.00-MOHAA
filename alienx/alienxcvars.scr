// BASEBUILDER CONSOLE COMMANDS:
//	alx_basebuild 0 (turn off basebuilding immediately, respawns everyone)
//	alx_timeboost 120 (increase build time limit by 120 seconds, or any # of seconds)
//	alx_timedown 120 (decrease build time limit by 120 seconds, or any # of seconds)
//	alx_objectlimitboost 200 (increase basebuild object limit by 200, or any # of objects)
//	alx_objectlimitdown 200 (decrease basebuild object limit by 200, or any # of objects)
//
//	alx_removeall [27 or Searingwolfe] (remove everything placed by $player[27] or by Searingwolfe). Using players' names only works in MOHAA Reborn 1.12
//	alx_removeobjects [27 or Searingwolfe] (remove all static objects placed by $player[27] or by Searingwolfe)
//	alx_removeturrets [27 or Searingwolfe] (remove all TurretGun objects placed by $player[27] or by Searingwolfe)
//	alx_removelifts [27 or Searingwolfe] (remove all lifts placed by $player[27] or by Searingwolfe)
//	alx_removeteles [27 or Searingwolfe] (remove all teles placed by $player[27] or by Searingwolfe)
//
// When placing down objects, hold the FIRE button to hold still the nonsolid object, and let go of FIRE to place down the object.
// If holding FIRE for 3+ seconds while placing down an object, placement will be cancelled (laser will turn green). Press FIRE again to uncancel (place down now).
//
// While holding object & holding USE(e) key, lean left/right to change object angles. Hold lean left/right for 2 seconds to slowly trim object's angles.
//
// Hold Right-Click to change build-laser from green to blue; this activates "Trimming Mode" for the object being held (works for Reborn only).
// In trimming mode, players can lean left/right to slowly adjust roll-angles (x,y,z angles = pitch,yaw,roll).
// In trimming mode, players can crouch to rotate the object's yaw-angle by 90 degrees, flipping the lean left/right angles to adjust pitch-angles (but still looks like adjusting roll-angles from player's point of view).
// In trimming mode, players can walk (not run) forward/backward to increase/decrease their build-laser's distance between them and their object being held.
// When a new object is picked up, their build-laser's distance is automatically reset back to "level.basebuild_laserdist" (default = 350).
//
// Hold USE(e) + Lean left/right to activate a yellow "Builder Remover Laser".
// Hold USE(e) + hold FIRE at an object, lift, or tele that you placed to pick it up again (laser will turn red when picking up).
// Players can only pick up their own objects, unless logged in as admin (isadmin = 1).
// 
// While holding an object or lift: hold FIRE, then hold USE(e) to remove it. While holding a tele: hold USE(e), then hold FIRE to remove it.
//
// Basebuilder script automatically lowers "objectlimit" count to avoid going beyond 1024 maxentities.
// Mini-flak88 objects act as flamethrowers, killing players within 5 hits. 

main:

	thread alx_basebuild

	thread alx_timeboost
	thread alx_timedown
	thread alx_objectlimitboost
	thread alx_objectlimitdown

	thread alx_removeall
	thread alx_removeobjects
	thread alx_removeturrets
	thread alx_removelifts
	thread alx_removeteles
end

////////////////////////////////////////////////////////////////////////

alx_basebuild:

	while(getcvar("alx_basebuild") != "0") { waitframe }

	if(getcvar("alx_basebuild") == "0")
	{
		println("----- Admin has canceled the basebuild.")
	}
end

////////////////////////////////////////////////////////////////////////

alx_timeboost:

	local.cvar = getcvar("alx_timeboost")
	while(getcvar("alx_timeboost") == local.cvar) { waitframe }

	if(int(getcvar("alx_timeboost")) > 0)
	{
		println("----- Increasing build time from " + level.bb_timeleft + " to " + (level.bb_timeleft + int(getcvar("alx_timeboost"))) + " seconds.")
	}

	thread alx_timeboost
end

alx_timedown:

	local.cvar = getcvar("alx_timedown")
	while(getcvar("alx_timedown") == local.cvar) { waitframe }

	if(int(getcvar("alx_timedown")) > 0)
	{
		println("----- Decreasing build time from " + level.bb_timeleft + " to " + (level.bb_timeleft - int(getcvar("alx_timedown"))) + " seconds.")
	}

	thread alx_timedown
end

alx_objectlimitboost:

	local.cvar = getcvar("alx_objectlimitboost")
	while(getcvar("alx_objectlimitboost") == local.cvar) { waitframe }

	if(int(getcvar("alx_objectlimitboost")) > 0 && level.time >= 5)
	{
		println("----- Increasing object limit from " + level.objectlimit + " to " + (level.objectlimit + int(getcvar("alx_objectlimitboost"))))
		println("----- CAUTION: Going above 1024 max entities on the map will cause the server to crash!")
	}

	thread alx_objectlimitboost
end

alx_objectlimitdown:

	local.cvar = getcvar("alx_objectlimitdown")
	while(getcvar("alx_objectlimitdown") == local.cvar) { waitframe }

	if(int(getcvar("alx_objectlimitdown")) > 0 && level.time >= 5)
	{
		println("----- Decreasing object limit from " + level.objectlimit + " to " + (level.objectlimit - int(getcvar("alx_objectlimitdown"))))
	}

	thread alx_objectlimitdown
end

////////////////////////////////////////////////////////////////////////

alx_removeall:

	while(getcvar("alx_removeall") == "0" || getcvar("alx_removeall") == "") { waitframe }

	local.cvar = getcvar("alx_removeall")
	if(int(local.cvar) < 0) { local.cvar = local.cvar * -1 }

	local.c = 0
	for(local.i = 1; local.i <= $player.size; local.i++)
	{
		if($player[local.i] != NULL && (local.cvar == netname $player[local.i] || int(local.cvar) == local.i))
		{
			$player[local.i] iprint "----- Admin has removed everything that you placed down in Base Builder."

			for(local.b = 1; local.b <= $player[local.i].bb_ents.size; local.b++)
			{
				if($player[local.i].bb_ents[local.b].bipod != NIL && $player[local.i].bb_ents[local.b].bipod != NULL)
				{
					$player[local.i].bb_ents[local.b].bipod remove
					level.objectlimitcount--
					local.c++
				}
				$player[local.i].bb_ents[local.b] remove
				level.objectlimitcount--
				local.c++
			}
			println("----- Removed " + local.c + " entities placed by: " + netname $player[local.i])
		}
	}

	if(getcvar("alx_removeall") != "0") { setcvar "alx_removeall" "0" }
	thread alx_removeall
end

alx_removeobjects:

	while(getcvar("alx_removeobjects") == "0" || getcvar("alx_removeobjects") == "") { waitframe }

	local.cvar = getcvar("alx_removeobjects")
	if(int(local.cvar) < 0) { local.cvar = local.cvar * -1 }

	local.c = 0
	for(local.i = 1; local.i <= $player.size; local.i++)
	{
		if($player[local.i] != NULL && (local.cvar == netname $player[local.i] || int(local.cvar) == local.i))
		{
			$player[local.i] iprint "----- Admin has removed all static objects that you placed down in Base Builder."

			for(local.b = 1; local.b <= $player[local.i].bb_ents.size; local.b++)
			{
				if($player[local.i].bb_ents[local.b] != NULL)
				{
					if($player[local.i].bb_ents[local.b].islift != 1 && $player[local.i].bb_ents[local.b].istele != 1)
					{
						if($player[local.i].bb_ents[local.b].bipod != NIL && $player[local.i].bb_ents[local.b].bipod != NULL)
						{
							$player[local.i].bb_ents[local.b].bipod remove
							level.objectlimitcount--
							local.c++
						}
						$player[local.i].bb_ents[local.b] remove
						level.objectlimitcount--
						local.c++
					}
				}
			}
			println("----- Removed " + local.c + " static objects placed by: " + netname $player[local.i])
		}
	}

	if(getcvar("alx_removeobjects") != "0") { setcvar "alx_removeobjects" "0" }
	thread alx_removeobjects
end

alx_removeturrets:

	while(getcvar("alx_removeturrets") == "0" || getcvar("alx_removeturrets") == "") { waitframe }

	local.cvar = getcvar("alx_removeturrets")
	if(int(local.cvar) < 0) { local.cvar = local.cvar * -1 }

	local.c = 0
	for(local.i = 1; local.i <= $player.size; local.i++)
	{
		if($player[local.i] != NULL && (local.cvar == netname $player[local.i] || int(local.cvar) == local.i))
		{
			$player[local.i] iprint "----- Admin has removed all Turret Guns that you placed down in Base Builder."

			for(local.b = 1; local.b <= $player[local.i].bb_ents.size; local.b++)
			{
				if($player[local.i].bb_ents[local.b] != NULL)
				{
					if($player[local.i].bb_ents[local.b].islift != 1 && $player[local.i].bb_ents[local.b].istele != 1)
					{
						if($player[local.i].bb_ents[local.b].bipod != NIL && $player[local.i].bb_ents[local.b].bipod != NULL)
						{
							$player[local.i].bb_ents[local.b].bipod remove
							$player[local.i].bb_ents[local.b] remove
							level.objectlimitcount -= 2
							local.c += 2
						}
					}
				}
			}
			println("----- Removed " + local.c + " Turret Guns + bipods placed by: " + netname $player[local.i])
		}
	}

	if(getcvar("alx_removeturrets") != "0") { setcvar "alx_removeturrets" "0" }
	thread alx_removeturrets
end

alx_removelifts:

	while(getcvar("alx_removelifts") == "0" || getcvar("alx_removelifts") == "") { waitframe }

	local.cvar = getcvar("alx_removelifts")
	if(int(local.cvar) < 0) { local.cvar = local.cvar * -1 }

	local.c = 0
	for(local.i = 1; local.i <= $player.size; local.i++)
	{
		if($player[local.i] != NULL && (local.cvar == netname $player[local.i] || int(local.cvar) == local.i))
		{
			$player[local.i] iprint "----- Admin has removed all lifts that you placed down in Base Builder."

			for(local.b = 1; local.b <= $player[local.i].bb_ents.size; local.b++)
			{
				if($player[local.i].bb_ents[local.b] != NULL)
				{
					if($player[local.i].bb_ents[local.b].islift == 1)
					{
						$player[local.i].bb_ents[local.b] remove
						level.objectlimitcount--
						local.c++
					}
				}
			}
			println("----- Removed " + local.c + " lifts placed by: " + netname $player[local.i])
		}
	}

	if(getcvar("alx_removelifts") != "0") { setcvar "alx_removelifts" "0" }
	thread alx_removelifts
end

alx_removeteles:

	while(getcvar("alx_removeteles") == "0" || getcvar("alx_removeteles") == "") { waitframe }

	local.cvar = getcvar("alx_removeteles")
	if(int(local.cvar) < 0) { local.cvar = local.cvar * -1 }

	local.c = 0
	for(local.i = 1; local.i <= $player.size; local.i++)
	{
		if($player[local.i] != NULL && (local.cvar == netname $player[local.i] || int(local.cvar) == local.i))
		{
			$player[local.i] iprint "----- Admin has removed all teleporters that you placed down in Base Builder."

			for(local.b = 1; local.b <= $player[local.i].bb_ents.size; local.b++)
			{
				if($player[local.i].bb_ents[local.b] != NULL)
				{
					if($player[local.i].bb_ents[local.b].istele == 1)
					{
						$player[local.i].bb_ents[local.b] remove
						level.objectlimitcount--
						local.c++
					}
				}
			}
			println("----- Removed " + local.c + " teleporters placed by: " + netname $player[local.i])
		}
	}

	if(getcvar("alx_removeteles") != "0") { setcvar "alx_removeteles" "0" }
	thread alx_removeteles
end